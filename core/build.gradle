plugins {
  id("com.starter.library.android")
  id("com.starter.publishing")
}

android {
  defaultConfig {
    minSdk 23
    testInstrumentationRunner "com.usefulness.testing.screenshot.CustomScreenshotTestRunner"
  }
  publishing {
    singleVariant("release")
  }
  packagingOptions {
    exclude 'LICENSE.txt'
  }
  lint {
    baseline = file("lint-baseline.xml")
  }
}

androidComponents {
  beforeVariants(selector().withBuildType("release")) { builder ->
    builder.enable = true
  }
}

tasks.withType(JavaCompile).configureEach {
  options.release.set(JavaVersion.VERSION_1_8.majorVersion.toInteger())
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  testLogging {
    events "skipped", "failed"
  }
  doLast {
    Thread.sleep(2000) // https://github.com/gradle/gradle/issues/16603
  }
}

dependencies {
  implementation(libs.androidx.annotation)
  implementation(libs.androidx.core)
  implementation(libs.gson)

  testImplementation(libs.junit.jupiter)
  testImplementation(libs.assertj.core)

  androidTestImplementation(libs.assertj.core)
  androidTestImplementation(libs.espresso.core)
  androidTestImplementation(libs.androidtest.core)
  androidTestImplementation(libs.androidtest.runner)
  androidTestImplementation(libs.androidtest.rules)
  androidTestImplementation(libs.androidtest.junitext)
}
