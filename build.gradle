import org.jetbrains.kotlin.gradle.dsl.KotlinCompile
import org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapperKt

plugins {
  alias(libs.plugins.starter.config)
  alias(libs.plugins.starter.versioning)
  alias(libs.plugins.osacky.doctor)
  alias(libs.plugins.starter.library.android) apply false
  alias(libs.plugins.starter.library.kotlin) apply false
}

commonConfig {
  javaFilesAllowed true
  javaVersion JavaVersion.VERSION_11
}

allprojects {
  pluginManager.withPlugin("kotlin") { plugin ->
    def kotlinVersion = KotlinPluginWrapperKt.getKotlinPluginVersion(project)
    configurations.configureEach {
      resolutionStrategy.eachDependency {
        if (requested.group == 'org.jetbrains.kotlin' && requested.name.startsWith("kotlin")) {
          useVersion kotlinVersion
        }
      }
    }
    kotlin {
      jvmToolchain(17)
    }

    def targetJavaVersion = JavaVersion.VERSION_11
    tasks.withType(JavaCompile).configureEach {
      options.release.set(targetJavaVersion.majorVersion.toInteger())
    }
    tasks.withType(KotlinCompile).configureEach {
      kotlinOptions.jvmTarget = targetJavaVersion
    }
  }
}
